import org.jsweet.transpiler.EcmaScriptComplianceLevel;

apply plugin: 'java'

description = """A set of examples to illustrate JSweet"""

sourceCompatibility = 11
targetCompatibility = 11

buildscript {
    ext {
        JSweetVersion = "3.1.0-SNAPSHOT"
    }

    boolean snapshotMode = true;
    project.ext.repoType =  snapshotMode ? 'snapshot' : 'release';

    println "repoType=" + project.repoType + " JSweetVersion=${JSweetVersion}"

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repository.jsweet.org/artifactory/libs-" + project.repoType + "-local" }
        maven { url "https://repository.jsweet.org/artifactory/plugins-" + project.repoType + "-local" }
        maven { url "https://repository.jsweet.org/artifactory/libs-release-local" }
        maven { url "https://repository.jsweet.org/artifactory/plugins-release-local" }
    }
    dependencies {
        classpath("org.jsweet:jsweet-gradle-plugin:" + JSweetVersion) { transitive = true }
    }
}

repositories {
    mavenCentral()
    maven { url "https://repository.jsweet.org/artifactory/libs-" + project.repoType + "-local" }
    maven { url "https://repository.jsweet.org/artifactory/libs-release-local" }
}

dependencies {
    implementation group: 'org.jsweet', name: 'jsweet-core', version: "6.3.0"
    implementation group: 'org.jsweet.candies', name: 'sprintf-js', version: '0.0.0-1.1.0-20160525'
    implementation(group: 'org.jsweet.candies', name: 'sqlite3', version: '2.2.3-1.1.0-20160525')
}

compileJava {
    enabled = false
}

apply plugin: 'org.jsweet.jsweet-gradle-plugin'

jsweet {
    verbose = true
    encoding = 'UTF-8'
    sourceMap = true
    tscWatchMode = true
    outDir = project.file('www/dist')
    tsOut = project.file('temp/ts')
    bundle = true;
    targetVersion = EcmaScriptComplianceLevel.ES6
    includes = ['**/*.java']
    extraSystemPath="/home/avramenko/.nvm/versions/node/v14.21.2/bin"
}